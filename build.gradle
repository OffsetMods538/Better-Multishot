plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'io.github.dexman545.outlet' version '1.5.1'
	id 'com.modrinth.minotaur' version '2.+'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "bettermultishot"
version = "${project.mod_version}+${project.minecraft_version}"
outlet.mcVersionRange = project.supported_minecraft_versions


configurations {
	includeModImplementation

	include.extendsFrom           includeModImplementation
	modImplementation.extendsFrom includeModImplementation


	includeImplementation

	include.extendsFrom        includeImplementation
	implementation.extendsFrom includeImplementation
}

repositories {
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
		content {
			includeGroup "com.github.llamalad7.mixinextras"
			includeGroup "top.offsetmonkey538"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${outlet.yarnVersion(project.minecraft_version)}:v2"
	modImplementation "net.fabricmc:fabric-loader:${outlet.loaderVersion()}"


	// Mixin Extras
	includeImplementation annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixinextras_version}")

	// Config library
	includeModImplementation "top.offsetmonkey538:MonkeyConfig538:${project.monkeyconfig538_version}"
}

processResources {
	inputs.properties(Map.of(
			"version",                    project.mod_version,
			"monkeyconfig538_version",    project.monkeyconfig538_version,
			"supportedMinecraftVersions", project.supported_minecraft_versions
	))

	filesMatching("fabric.mod.json") {
		expand(Map.of(
				"version",                    project.mod_version,
				"monkeyconfig538_version",    project.monkeyconfig538_version,
				"supportedMinecraftVersions", project.supported_minecraft_versions
		))
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "bettermultishot"
	def customVersionName = System.getenv("VERSION_NAME")
	if (customVersionName != null) versionName = customVersionName
	versionNumber = "${project.version}"
	versionType = "beta"
	uploadFile = remapJar.archiveFile
	additionalFiles = [sourcesJar.archiveFile]
	gameVersions = outlet.mcVersions()
	syncBodyFrom = rootProject.file("README.md").text
	def changelogFile = rootProject.file("CHANGELOG.md")
	if (changelogFile.exists()) {
		changelog = changelogFile.text
	}

	dependencies {
		embedded.project "monkeyconfig538"
	}
}
